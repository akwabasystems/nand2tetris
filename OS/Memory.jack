/**
 * This library provides two services: direct access to the computer's main memory (RAM), and allocation 
 * and recycling of memory blocks. The Hack RAM consists of 32,768 words, each holding a 16-bit binary number.
 */ 
class Memory {

    static Array RAM;

    /** 
	 * Initializes the class. 
	 */
    function void init() {
        let RAM = 0;
        let RAM[2048] = 14334;
        let RAM[2049] = 2050;
        return;
    }


	/**
	 * Returns the RAM value at the given address. 
	 *
	 * @param address		the address for which to return value
	 * @return the RAM value at the given address
	 */
    function int peek(int address) {
        return RAM[address];
    }


    /** 
     * Sets the RAM value at the given address to the given value. 
     *
     * @param address		the address for which to set the value
     * @param value			the value to set for the given address
     */
    function void poke(int address, int value) {
	    let RAM[address] = value;
	    return;
    }


    /** 
     * Finds an available RAM block of the given size and returns a reference to its base address. 
     *
     * @param size		the size of the RAM block to allocate
     * @return a reference to the base address of the allocated block
     */
    function int alloc(int size) {
        var Array index;

        if (size < 1) {
            do Sys.error(5);
        }

        let index = 2048;

        while (index[0] < size) {
            let index = index[1];
        }

        if ((index + size) > 16379) {
            do Sys.error(6);
        }

        if (index[0] > (size + 2)) {
            let index[size + 2] = (index[0] - size - 2);

            if (index[1] = (index + 2)) {
                let index[size + 3] = (index + size + 4);
            } else {
                let index[size + 3] = index[1];
            }

            let index[1] = index + size + 2;
        }

        let index[0] = 0;

        return (index + 2);
    }


    /** 
     * De-allocates the given object (cast as an array) by making it available for future allocations. 
     *
     * @param object		the object to deallocate
     */
    function void deAlloc(Array object) {
        var Array index, j;

        let index = object - 2;
        let j = index[1];

        if (j[0] = 0) {
            let index[0] = index[1] - index - 2;
        } else {
            let index[0] = index[1] - index + j[0];
            
            if (j[1] = (j + 2)) {
                let index[1] = index + 2;
            } else {
                let index[1] = j[1];
            }
        }

        return;
    }

}

